plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystorePropertiesFile.withReader('UTF-8') { reader ->
        keystoreProperties.load(reader)
    }
}

def _keyAlias = System.getenv("KEY_ALIAS") ?: keystoreProperties.getProperty("keyAlias")
def _keyPassword = System.getenv("KEY_PASSWORD") ?: keystoreProperties.getProperty("keyPassword")
def _storeFile = file(System.getenv("KEYSTORE") ?: keystoreProperties.getProperty("storeFile") ?: "keystore.jks")
def _storePassword = System.getenv("KEYSTORE_PASSWORD") ?: keystoreProperties.getProperty("storePassword")


android {
    ndkVersion "25.2.9519653"
    compileSdkVersion 34

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }


    defaultConfig {
        applicationId "com.honjow.fehviewer"
        minSdkVersion 21
        targetSdkVersion 33
//        multiDexEnabled true
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        archivesBaseName = "Eros-FE_$versionName+$versionCode"
        splits {
            abi {
                enable true
                reset()
                universalApk true
                //noinspection ChromeOsAbiSupport
                include "armeabi-v7a", "arm64-v8a", "x86_64"
            }
        }
    }
    signingConfigs {
        debug {
            storeFile _storeFile
            storePassword _storePassword
            keyAlias _keyAlias
            keyPassword _keyPassword
            v1SigningEnabled true
            v2SigningEnabled true
        }
        release {
            storeFile _storeFile
            storePassword _storePassword
            keyAlias _keyAlias
            keyPassword _keyPassword
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            if (signingConfigs.release.storePassword != null){
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
        }
        debug {
            if (signingConfigs.debug.storePassword != null){
                signingConfig signingConfigs.debug
            }
            minifyEnabled true
            shrinkResources true
        }
    }
    namespace 'com.honjow.fehviewer'
    lint {
        disable 'InvalidPackage'
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.7.20"
    implementation "androidx.startup:startup-runtime:1.1.1"
    implementation 'com.google.mlkit:barcode-scanning:17.2.0'
}